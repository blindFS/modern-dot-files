#!/usr/bin/env nu

# This is a demo config to showcase some of the most important commands.
# It is meant to be changed and configured, as it is intentionally kept sparse.
# For a (much) more advanced configuration example see my dotfiles:
# https://github.com/FelixKratz/dotfiles

let plugin_dir = $"($env.CONFIG_DIR)/plugins"
source config.nu

(sketchybar
  --bar position=top height=25 blur_radius=2 color=0x00000000
  --add event aerospace_workspace_change 
  --add event toggle_stats
  --default ...($config.default | arg_to_setting $plugin_dir)
)

##### Aerospace integration #####

def args_per_workspace [it: string] {
  ['--add' 'item' $"space.($it)" 'left']
  | append ['--set' $"space.($it)" $"label=($it)" $"click_script=aerospace workspace ($it)"]
  | append ($config.workspace_default_args | arg_to_setting $plugin_dir)
}

let workspace_args = (aerospace list-workspaces --all
  | lines
  | each {|it| args_per_workspace $it})
  | flatten

(sketchybar ...($workspace_args)
  --add item ws_listener left
  --subscribe ws_listener aerospace_workspace_change space_windows_change
  --set ws_listener drawing=off updates=on script=($plugin_dir)/aerospace.nu
)

sketchybar ...($config.plugin_configs
              | each {|it| $it
                | build_sketchybar_args $plugin_dir}
                | flatten)

##### Force all scripts to run the first time (never do this in a script) #####
sketchybar --update
